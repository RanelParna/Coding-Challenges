(* Challenge: creating a function, SteinerInellipseArea, that computes the area of the 
Steiner inellipse of a given triangle. The Steiner inellipse is a unique ellipse that is inscribed 
in a triangle, touching the midpoints of each side. *)

(* Vertices  of  the  triangles  as  complex  numbers *)
z1 = -1.5;
z2 = 1.5;
z3 = 1.5 I;
(* Utility function to get (x,y) from complex number z *)
xy[z_] := {Re[z], Im[z]}
(* Steiner polynomial *)
p = (z - z1) (z - z2) (z - z3);
(* Derivative is a quadratic polynomial *)
pd = Simplify[D[p, z]];
(* The foci f1 and f2 of the Steiner Innelipse are given by the roots \
of p *)
{f1, f2} = z /. Solve[pd == 0, z];
f1;
f2;
(* Ellipse centre *)
centre = (f1 + f2)/2;
(* The points of tangency v1,v2,v3 are at the mid-points of the \
triangles sides *)
v1 = (z1 + z2)/2;
v2 = (z2 + z3)/2;
v3 = (z1 + z3)/2;
(* Compute the equation of the ellipse *)
l = N[Abs[f1 - v1] + Abs[f2 - v1]];
d1 = {x, y} - N[xy[f1]];
d2 = {x, y} - N[xy[f2]];
eq = Simplify[Sqrt[Dot[d1, d1]] + Sqrt[Dot[d2, d2]]] - l;

(* Computing the area *)

a = l/2 ;(* Semi-major axis *)
c = N[Abs[f1 - centre]]; (* Eccentricty *)
b = Sqrt[a^2 - c^2];
area = Pi*a*b ;

Print["The area is: ", area]

(* This plots the result *)

ellG = ContourPlot[eq == 0, {x, -15, 15}, {y, -15, 15}, 
   PlotPoints -> 50];
fG = Graphics[{Blue, PointSize[Medium], Point[{xy[f1], xy[f2]}]}];
triangleG = 
  Graphics[{LightGray, Triangle[{{xy[z1], xy[z2], xy[z3]}}]}];
verticesG = 
  Graphics[{Black, PointSize[Medium], 
    Point[{xy[z1], xy[z2], xy[z3]}]}];
tangenciesG = 
  Graphics[{Red, PointSize[Medium], 
    Point[{xy[v1], xy[v2], xy[v3]}]}];
centreG = Graphics[{Red, PointSize[Medium], Point[xy[centre]]}];
Show[triangleG, verticesG, tangenciesG, centreG, fG, ellG]


SteinerInEllipseArea[triangle_] := 
  Module[{z1, z2, z3, x1, y1, x2, y2, x3, y3, f1, f2, centre, v1, v2, 
    v3, d1, d2, l, a, b, c, area, p, pd, z},
   {{x1, y1}, {x2, y2}, {x3, y3}} = triangle;
   z1 = x1 + y1*I;
   z2 = x2 + y2*I;
   z3 = x3 + y3*I;
   p = (z - z1) (z - z2) (z - z3);
   (* Derivative is a quadratic polynomial *)
   pd = Simplify[D[p, z]];
   (* The foci f1 and f2 of the Steiner Innelipse are given by the \
roots of p *)
   {f1, f2} = z /. Solve[pd == 0, z];
   (* Ellipse centre *)
   centre = (f1 + f2)/2;
   (* The points of tangency v1,v2,v3 are at the mid-
   points of the triangles sides *)
   v1 = (z1 + z2)/2;
   v2 = (z2 + z3)/2;
   v3 = (z1 + z3)/2;
   (* Compute the equation of the ellipse *)
   l = N[Abs[f1 - v1] + Abs[f2 - v1]];
   (* Computing the area *)
   a = l/2 ;(* Semi-major axis *)
   c = N[Abs[f1 - centre]]; (* Eccentricty *)
   b = Sqrt[a^2 - c^2]; (* Semi-mino axis *)
   area = Pi*a*b ;
   area
   ];
